{Application 'MENUCARTOGRAFO' logic file generated by CSPro}
PROC GLOBAL
string nombre_usuario = "",  rol_letras =  "";
numeric role_no = 0;
file pffFile;


function cerrarSesion()
	savesetting("login", "");
	savesetting("rol", "");
	savesetting("Usuario", "");
	execpff("../Login/Login.pff", stop);
    stop(1);
end;

function openFormFc01(string user_name = "", id_role = 0)
			string pffFilename = pathname(Application) + "../ENCOVIFC01/ENCOVIFC01.pff";
				if setfile(pffFile,pffFilename,create) = 0 then
					errmsg("Failed to open file %s", pffFilename);
				endif;
			filewrite(pffFile,"[Run Information]");
			filewrite(pffFile,"Version=CSPro 7.7");
			filewrite(pffFile,"AppType=Entry");
			filewrite(pffFile,"[Files]");
			
			filewrite(pffFile,"Application=" + pathname(Application) + "../ENCOVIFC01/ENCOVIFC01.ent");
			filewrite(pffFile,"InputData=" + pathname(Application) + "../Datos/Encovifc01.csdb");
			filewrite(pffFile,"[ExternalFiles]");
			filewrite(pffFile,"MUNICIPIOS_DICT=" + pathname(Application) + "../Datos/Municipios.dat");
			filewrite(pffFile,"SECTORES_DICT=" + pathname(Application) + "../Datos/Sectores.dat");

			filewrite(pffFile,"[Parameters]");
			//filewrite(pffFile,"OnExit=" + pathname(Application) + "../../Login/Login.ppf");
			filewrite(pffFile,"NameUser=%s", user_name);
			filewrite(pffFile,"IdRole=%d", id_role);
			close(pffFile);
			execpff(pffFilename, stop);
end;




PROC MENUCARTOGRAFO_FF


PROC MENUCARTOGRAFO_LEVEL
PREPROC
if loadsetting("login") <> "" and loadsetting("rol") <> ""   then
	if(tonumber(loadsetting("rol")) <> 5 and tonumber(loadsetting("rol")) <> 1) then
		warning("Permiso denegado!")
		select("Ok", continue)
		default(1);
		cerrarSesion();
	else
		MENUCARTOGRAFO_ID = tonumber(loadsetting("rol"))
	endif;
else
	warning("Permiso denegado!")
	select("Ok", continue)
	default(1);
	cerrarSesion();
endif;
PROC MENU_OPCION

PREPROC
if (loadsetting("login") <> "" and loadsetting("Usuario") <> "") then
	nombre_usuario = loadsetting("Usuario");
endif;

if(loadsetting("rol") <> "") then
	rol_letras = loadsetting("rol");
	role_no = tonumber(loadsetting("rol"));
endif;

POSTPROC

if($ = 9) then
	cerrarSesion();
elseif ($ = 1) then
	openFormFc01(nombre_usuario,role_no);

	//skip to FORM_UPM;
else
	warning("En contrucción");
	reenter
endif;
PROC DEPTO_MENU
PREPROC
valueset interview_vs;

forcase DEPARTAMENTOS_DICT(CaseStatus.NotDeleted) do
	    interview_vs.add(maketext("%d. %s", POSTAL,strip(DEPARTAMENTO)), POSTAL );
endfor;

setvalueset (@getsymbol(), interview_vs);
PROC MUPIO_MENU
PREPROC
valueset municipios;

forcase MUNICIPIOS_DICT(CaseStatus.NotDeleted) where DEPTO_ID = DEPTO_MENU do
	    municipios.add(maketext("%d. %s", MUNICIPIO_ID,strip(NOMBRE_MUNICIPIO)), MUNICIPIO_ID );
endfor;

setvalueset (@getsymbol(), municipios);
PROC SECTOR_MENU
PREPROC
valueset db_sectores;

forcase SECTORES_DICT(CaseStatus.NotDeleted) where MUNICIPIOS_MUNICIPIO_ID = MUPIO_MENU AND DEPARTAMENTOS_DEPTO_ID = DEPTO_MENU  do
	    db_sectores.add(maketext("%d. %s", SECTORES_ID,strip(NOMBRE_SECTOR)), SECTORES_ID );
endfor;

setvalueset (@getsymbol(), db_sectores);
PROC AREA_MENU
PREPROC
numeric encontrado = 0;
forcase SECTORES_DICT(CaseStatus.NotDeleted) where MUNICIPIOS_MUNICIPIO_ID = MUPIO_MENU AND DEPARTAMENTOS_DEPTO_ID = DEPTO_MENU AND SECTORES_ID = SECTOR_MENU   do
	    AREA_MENU = AREA_ID;
		encontrado = 1;
		noinput;
endfor;

IF encontrado = 0 THEN
	valueset setArea;
	setArea.add("1. URBANA", 1);
	setArea.add("2. RURAL", 2);
	setvalueset (@getsymbol(), setArea);
ENDIF;
PROC CATEGORIA_MENU
PREPROC
valueset db_categorias;

forcase CATEGORIAS_DICT(CaseStatus.NotDeleted)  do
	    db_categorias.add(maketext("%d. %s", CATEGORIAS_ID,strip(NOMBRE_CATEGORIA)), CATEGORIAS_ID );
endfor;

setvalueset (@getsymbol(), db_categorias);
PROC COD_CARTOGRAFO_MENU
PREPROC
if loadsetting("login") <> "" then
    $ = tonumber(loadsetting("login"));
    noinput;
endif;
PROC LUGAR_POBLADO_MENU

POSTPROC
	savesetting("departamento", maketext("%d", DEPTO_MENU));
	savesetting("municipio", maketext("%d", MUPIO_MENU));
	savesetting("sector", maketext("%d", SECTOR_MENU));
	savesetting("area", maketext("%d", AREA_MENU));
	savesetting("categoria", maketext("%d", CATEGORIA_MENU));
	savesetting("cod_supervisor", maketext("%d", COD_SUPERVISOR_MENU));
	savesetting("cod_cartografo", maketext("%d", COD_CARTOGRAFO_MENU));
	savesetting("lugar_poblado", LUGAR_POBLADO_MENU);
	openFormFc01(nombre_usuario,role_no);
